############################################################
#             Source: http://libuv.org/                    #
############################################################

set (ATFRAME_THIRD_PARTY_LIBUV_VERSION  "1.27.0")
set (ATFRAME_THIRD_PARTY_LIBUV_PKG_DIR  "${ATFRAME_THIRD_PARTY_TARGET_LIBUV_BUILD_DIR}/source")
set (ATFRAME_THIRD_PARTY_LIBUV_PKG_PATH "${ATFRAME_THIRD_PARTY_LIBUV_PKG_DIR}/libuv-v${ATFRAME_THIRD_PARTY_LIBUV_VERSION}.tar.gz")
set (ATFRAME_THIRD_PARTY_LIBUV_SRC_DIR  "${ATFRAME_THIRD_PARTY_LIBUV_PKG_DIR}/libuv-v${ATFRAME_THIRD_PARTY_LIBUV_VERSION}")

if (NOT EXISTS ${ATFRAME_THIRD_PARTY_LIBUV_PKG_DIR})
    file (MAKE_DIRECTORY ${ATFRAME_THIRD_PARTY_LIBUV_PKG_DIR})
endif ()

if (NOT EXISTS ${ATFRAME_THIRD_PARTY_LIBUV_SRC_DIR})
    if (NOT EXISTS ${ATFRAME_THIRD_PARTY_LIBUV_PKG_PATH})
        file(DOWNLOAD "https://dist.libuv.org/dist/v${ATFRAME_THIRD_PARTY_LIBUV_VERSION}/libuv-v${ATFRAME_THIRD_PARTY_LIBUV_VERSION}.tar.gz" ${ATFRAME_THIRD_PARTY_LIBUV_PKG_PATH} SHOW_PROGRESS)
    endif ()

    execute_process(
        COMMAND ${CMAKE_COMMAND} -E tar xvf ${ATFRAME_THIRD_PARTY_LIBUV_PKG_PATH}
        WORKING_DIRECTORY ${ATFRAME_THIRD_PARTY_LIBUV_PKG_DIR}
    )
endif ()

if (NOT EXISTS ${ATFRAME_THIRD_PARTY_LIBUV_SRC_DIR})
    message(FATAL_ERROR "${ATFRAME_THIRD_PARTY_LIBUV_SRC_DIR} not found.")
endif ()

unset (ATFRAME_THIRD_PARTY_LIBUV_BUILD_OPTIONS)

list (APPEND ATFRAME_THIRD_PARTY_LIBUV_BUILD_OPTIONS "-DBUILD_TESTING=OFF")
if (NOT UNIX)
    list (APPEND ATFRAME_THIRD_PARTY_LIBUV_BUILD_OPTIONS DISABLE_INSTALL)
endif ()

# standard cmake project
ATPBTargetBuildThirdPartyByCMake(${ATFRAME_THIRD_PARTY_LIBUV_SRC_DIR} 
    ${ATFRAME_THIRD_PARTY_LIBUV_BUILD_OPTIONS}
)

if (NOT UNIX)
    include(GNUInstallDirs)
    if (NOT EXISTS "${ATFRAME_THIRD_PARTY_TARGET_LIBUV_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}")
        file (MAKE_DIRECTORY "${ATFRAME_THIRD_PARTY_TARGET_LIBUV_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}")
    endif ()
    if (NOT EXISTS "${ATFRAME_THIRD_PARTY_TARGET_LIBUV_INSTALL_PREFIX}/include")
        file (MAKE_DIRECTORY "${ATFRAME_THIRD_PARTY_TARGET_LIBUV_INSTALL_PREFIX}/include")
    endif ()
    add_custom_target(install-libuv ALL 
        ${CMAKE_COMMAND} -E copy_directory "${ATFRAME_THIRD_PARTY_LIBUV_SRC_DIR}/include" "${ATFRAME_THIRD_PARTY_TARGET_LIBUV_INSTALL_PREFIX}/include"
        COMMAND ${CMAKE_COMMAND} -E copy "${ATFRAME_THIRD_PARTY_BUILD_WORK_DIR}/${CMAKE_BUILD_TYPE}/uv_a.lib" "${ATFRAME_THIRD_PARTY_TARGET_LIBUV_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}/uv_a.lib"
        DEPENDS libuv
    )

    set_property(TARGET install-libuv PROPERTY FOLDER "install/${ATFRAME_THIRD_PARTY_TARGET_RELATIVE_ROOT_MODULE}")
endif ()
